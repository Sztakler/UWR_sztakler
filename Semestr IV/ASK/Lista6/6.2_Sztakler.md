# Zadanie 2
![](https://i.imgur.com/FRht91h.png)

Przeczytawszy *System V ABI* można zauważyć, że jeśli wynik funkcji nie mieści się w dwóch rejestrów, należy zwracać go przez pamięć, tj. funkcja wołająca musi zarezerwować pamięć na przechowanie wyniku oraz wskaźnika na ten obszar pamięci. Ten wskaźnik zostaje przekazany funkcji jako pierwszy, niejawny argument funkcji wołanej (`%rdi`). Wskaźnik jest zwracany przez `%rax`. Stąd:
* `RDI` -- ukryty argument, wskaźnik na zmienną typu `T*`
* `RSI` -- argument `*a`
* `RDX` -- argument `n`

```python3-
puzzle8:
        movq %rdx, %r11            #%r11 = n
        xorl %r10d, %r10d          #zerowanie %r10, teraz i
        xorl %eax, %eax            #zerowanie %rax, teraz sum
        movq $LONG_MIN, %r8        #max = LONG_MIN
        movq $LONG_MAX, %r9        #min = LONG_MAX
.L2:    cmpq %r11, %r10            #ogranicznik pętli
        jge .L5                    #jeśli i >= n
        movq (%rsi,%r10,8), %rcx   #pobieramy i-ty element tablicy a
        cmpq %rcx, %r9             #porównujemy z min
        cmovg %rcx, %r9            #jeśli min jest wiekszy, to min=a[i]
        cmpq %rcx, %r8             #porównujemy z max
        cmovl %rcx, %r8            #jeśli max jest mniejszy, to max=a[i]
        addq %rcx, %rax            #sum+=a[i] -- dodajemy element do sumy
        incq %r10                  #i++
        jmp .L2                    #skok do początku pętli
.L5:    cqto                       #sign-extend rax ->rdx:rax
        movq %r9, (%rdi)           #dodanie min do wyniku -> s.min = min
        idivq %r11                 # dzielenie rdx:rax(sum) / n
        movq %r8, 8(%rdi)          #dodanie max do wyniku -> s.max = max
        movq %rax, 16(%rdi)        #s.average = sum / n
        movq %rdi, %rax            #return s*
        ret
```

### Odpowiednik w C

```c=
struct T
{
   long min;
   long max;
   long average;
}

struct T puzzle8(long *a, long n)
{
   long sum = 0;
   long max = LONG_MIN;
   long min = LONG_MAX;
   
   for(int i = 0; i < n; i++)
   {
      long e = a[i];
      if(e > max) max = e;
      if(e < min) min = e;
      sum += e;
   }
   
   return (struct T) {.min = min, .max = max, .average = sum /n};
}
```

### Co robi procedura?
Znajduje minimum, maksimum oraz średnią zbioru $n$-elementowego i zapisuje wynik w krotce $3$-elementowej.

### Jaką sygnaturę można wywnioskować z kodu?
`struct T* puzzle(struct T *s, long *a, long n)`.
