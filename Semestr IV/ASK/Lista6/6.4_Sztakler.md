# Zadanie 4 
![](https://i.imgur.com/j6SBSL7.png)

```python=
M:   pushq %rdi            #argument n zapisany na stosie
     testq %rdi, %rdi      #n & n ? 
     je    .L2             #jeśli n==0 (ZF==1) skocz do return n
     leaq  -1(%rdi), %rdi  #argument w nowym wywołaniu to n-1
     call  M               #wywołanie temp1 = M(n-1)
     movq  %rax, %rdi      #wynik wywołania będzie argumentem kolejnego wywołania
     call  F               #wywołanie temp2 = F(temp1)
     movq  (%rsp), %rdi    #wyciągamy n ze stosu
     subq  %rax, %rdi      #zmniejszamy n o temp2, tzn. n = n - temp2
.L2: movq  %rdi, %rax      #zwracamy n
     ret

F:   testq %rdi, %rdi      # n & n ?
     je    .L3             #skocz jeśli n == 0
     movq  %rdi, %r12      #zapisujemy wartość n w r12
     leaq  -1(%rdi), %rdi  #obliczamy nowy argument n-1
     call  F               #wywołanie temp1 = F(n-1)
     movq  %rax, %rdi      #wynik temp2 = F(n-1) staje się nowym argumentem
     call M                #wywołanie M(temp2)
     subq  %rax, %r12      # n = n - temp2
     movq  %r12, %rax      # zwracamy n
     ret
L3:  movl  $1, %eax        #zwracamy 1
     ret
```

#### Konwencja wołania procedur
Na podstawie [rozdziału 1.3 i 1.4](https://aaronbloomfield.github.io/pdr/book/x86-64bit-ccc-chapter.pdf).

* przy wywołaniu procedury `%rsp` musi być podzielny przez `16`
* (**caller saved**) -- jeśli procedura wywołująca (**caller**) korzysta z rejestrów `%rax`, `%rdi`, `%rsi`, `%rdx`, `%rcx`, `%r8`, ..., `%r11`, to musi zapamiętywać stan tych rejestrów przed wywołanienm innej procedury, która z nich korzysta, ponieważ procedura wołana (**callee**) może zmodyfikować zawartość tych rejestrów bez ich zapisu.
* (**callee saved**) -- jeśli procedura wołana (**callee**) chce skorzystać z rejestru `%rbx`, `%r12`, `%r13`, `%r14`, `%rbp`, to musi zapamiętać ich wartości przed użyciem, a następnie przywrócić ich wartość przed zakończeniem swojego działania.
* Procedura przed zakończeniem musi oczywiścić stos z wartości, które na niego wstawiła.

#### Pierwszy błąd

W obrębie funkcji `M` odkładamy wartość na stos, ale nigdy nie zostaje ona z niego zdjęta, zatem adres powrotu z funkcji jest błędny (wczyta pozostawioną wartość, zamiast adresu return).

**Poprawka**

```python=
M:   pushq %rdi            
     testq %rdi, %rdi      
     je    .L2              
     leaq  -1(%rdi), %rdi  
     call  M               
     movq  %rax, %rdi      
     call  F               
     movq  (%rsp), %rdi    
     subq  %rax, %rdi       
.L2: movq  %rdi, %rax
     popq  %rdi
     ret
```

#### Drugi błąd

Funkcja `F` korzysta z rejestru `%r12`, by zachować wartość `n` z poprzedniego wywołania. Jest to rejestr `callee saved`, wiec musimy uważać, by zapamiętać wartość tego rejestru dla funcji wołającej (`caller`) i przy zakończeniu funkcji wołanej (`callee`), należy ją przywrócić.

**Poprawka**

```python=
F:   testq %rdi, %rdi      
     je    .L3             
     pushq %r12
     movq  %rdi, %r12      
     leaq  -1(%rdi), %rdi  
     call  F               
     movq  %rax, %rdi      
     call M                
     subq  %rax, %r12      
     movq  %r12, %rax
     popq  %r12
     ret
L3:  movl  $1, %eax
     ret
```
