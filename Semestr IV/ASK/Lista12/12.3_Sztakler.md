### Zadanie 3 

![](https://i.imgur.com/NRVUp9J.png)


Przeanalizujmy zoptymalizowany przez kompilator kod. Pamiętajmy, że kolejne argumenty funkcji są przekazywane przez odpowiednio `rdi`, `rsi`, `rdx`, `rcx`.
```asm=
foobar:
        test    rsi, rsi                # rsi <- rsi & rsi
        je      .L1                     # jeśli rsi równe 0, to skocz do ret
        sub     rdx, rcx                # rdx <- rdx - rcx
        lea     rax, [rdi+rsi*8]        # rax <- [rdi+rsi*8]
        imul    rdx, rdx                # rdx <- rdx * rdx
.L3:                                    # pętla
        mov     QWORD PTR [rdi], rdx    # [rdi] <- rdx
        add     rdi, 8                  # rdi <- rdi + 8
        add     rdx, 7                  # rdx <- rdx + 7
        cmp     rdi, rax                # rdi == rax ?
        jne     .L3                     # jeśli nie są równe to iterujemy dalej
.L1:                                    # jeśli równe to kończymy
        ret
```
        
Możemy przetłumaczyć te instrukcje na poniższy kod.

```c=
void foobarOpt(long a[], size_t n, long y, long z) {
    if (n == 0)
        return;
    
    y = (y - z);
    y = y * y;
    long* x = a;
    long* last = &a[n];
    do{
        *x = y;
        y = y + 7;
        x++;
    }
    while (x != last);
}


void foobar(long a[], size_t n, long y, long z) {
    for (int i = 0; i < n; i++) {
        long x = y - z;
        long j = 7 * i;
        a[i] = j + x * x;
    }
}
```

Po skompilowaniu obu tych procedur z opcją -O2 zostanie wygenerowany dokładnie ten sam kod w asemblerze.

```asm
foobar:
        testq   %rsi, %rsi
        je      .L8
        subq    %rcx, %rdx
        leaq    (%rdi,%rsi,8), %rax
        imulq   %rdx, %rdx
.L10:
        movq    %rdx, (%rdi)
        addq    $8, %rdi
        addq    $7, %rdx
        cmpq    %rax, %rdi
        jne     .L10
.L8:
        ret
foobarOpt:
        testq   %rsi, %rsi
        je      .L15
        subq    %rcx, %rdx
        leaq    (%rdi,%rsi,8), %rax
        imulq   %rdx, %rdx
.L17:
        movq    %rdx, (%rdi)
        addq    $8, %rdi
        addq    $7, %rdx
        cmpq    %rdi, %rax
        jne     .L17
.L15:
        ret
```

#### Niezmienniki pętli (ang. *loop invariant*)
**Niezmiennikiem pętli** nazywamy wartość lub warunek, który w każdej iteracji pętli pozostaje niezmieniony.

Zauważamy, że `x = y - z`, czyli jest różnicą zmiennych spoza ciała pęli. Oznacza to, że jego wartość jest identyczna w każdej iteracji pętli. W takim razie całe to wyrażenie możemy równie dobrze wynieść przed pętlę.

#### Zmienne indukcyjne (ang. *induction variable*)

**Zmienna indukcyjna** to zmienna, które zachowują się wewnątrz pętli jak funkcja liniowa -- co iterację zmieniają się o stałą wartość (`x = x + a`).

W kodzie z treści zadania możemy zauważyć dwie takie zmienne -- `i` oraz `j`.

#### Które wyrażenia zostały wyniesione przed pętlę i dlaczego?

Możemy zauważyć, że w zoptymalizowanym kodzie wyrażenie `long x = y - z;` zostało wyniesione przed ciało pętli. Wynika o stąd, że w obrębie pętli jego wartość pozostaje stała, zatem nie ma potrzeby wyliczać go przy każdej iteracji na nowo.

Dodatkowo, skoro `x` jest niezmiennikiem pętli, w takim razie `x * x`, występujące w linii `a[i] = j + x * x;` również nie musi być wyliczane w każdej iteracji. Możemy wyciągnąć je przed ciało pętli.

#### Które wyrażenia uległy osłabieniu?

**Osłabienie** (ang. *strength reduction*) jest metodą optymalizacji kodu, polegającą na zamianie kosztownych obliczeniowo operacji na ich tańsze odpowiedniki, np. mnożenie na dodawanie.

Wewnątrz kodu `foobar` mozemy znaleźć przykład takiego wyrażenia -- pierwotne
```
long j = 7 * i;
a[i] = j + x * x;
```
zostało zoptymalizowane do 

```
a[i] = y;
y = y + 7;
```

Kosztowna instrukcja mnożenia iteratora `i` przez `7` w każdym obrocie pętli została zamieniona na znacznie protszą do wykonania operację dodawania `7`.

